apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'de.felixschulze.gradle.hockeyapp'

def buildID() {
    try {
        Integer.parseInt("$System.env.TRAVIS_BUILD_NUMBER")
    } catch (ignored) {
        1
    }
}

def buildNote() {
    def formattedDate = new Date().format('MM/dd/yyyy')
    def lastCommitID = 'git rev-parse HEAD'.execute().text.trim()
    def lastCommitMsg = 'git log -1 --abbrev-commit'.execute().text.trim()
    "**Date**:$formattedDate\n\n" +
            "**Build ID**:${buildID()}\n\n" +
            "**Last git commit version**:$lastCommitID\n\n" +
            "**Last git commit message**:$lastCommitMsg"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.kotlinchina.smallpockets"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode buildID()
        versionName "1.0"
        // HockeyApp
        manifestPlaceholders = [HOCKEYAPP_APP_ID: 'afc9159ea81e4aaaaa37e4cdc4b62b2f']
    }
    signingConfigs {
        release {
            keyAlias 'SmallPocket'
            keyPassword 'kotlinchina'
            storeFile file('keystore/release.jks')
            storePassword 'kotlinchina'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"
    testCompile "org.mockito:mockito-core:1.+"
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'me.gujun.android.taggroup:library:1.4@aar'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'io.reactivex:rxkotlin:0.40.1'
    // JsoupXpath
    compile 'cn.wanghaomiao:JsoupXpath:0.2.2'
    // Realm
    compile "io.realm:realm-android-library:${realm_version}@aar"
    compile "io.realm:realm-annotations:${realm_version}"
    kapt "io.realm:realm-annotations:${realm_version}"
    kapt "io.realm:realm-annotations-processor:${realm_version}"
    // Adapter
    compile 'com.joanzapata.android:base-adapter-helper:1.1.11'
    // Evernote
    compile 'com.evernote:android-sdk:2.0.0-RC3'
    // HockeyApp
    compile 'net.hockeyapp.android:HockeySDK:3.7.1'
    // Apache commons
    compile 'commons-io:commons-io:2.4'

    compile 'com.android.support:design:23.4.0'

    compile 'com.melnykov:floatingactionbutton:1.3.0'

}

buildscript {
    ext.kotlin_version = '1.0.0'
    // Realm
    ext.realm_version = '0.87.4'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

hockeyapp {
    apiToken = "2586d0940aef4d6090e743ff141ee20d"
    releaseType = 2 // alpha
    notify = 0
    status = 2
    notesType = 1
    notes = buildNote()
}

repositories {
    mavenCentral()
}
